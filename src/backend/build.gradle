buildscript {
    ext {
        kotlinVersion = '1.3.61'
        springBootVersion = '2.1.5.RELEASE'
        springCloudVersion = 'Greenwich.SR2'

        ktlintVersion = "0.36.0"
        swaggerCoreVersion = "1.5.22"
        swaggerFoxVersion = "2.9.2"
        jasyptVersion = "2.1.2"
        awsS3Version = "1.11.631"
        innerCosVersion = "5.6.7"
        guavaVersion = "28.1-jre"
        commonsCodecVersion = "1.10"
        commonsIoVersion = "2.6"
        commonsUploadVersion = "1.4"
        skywalkingVersion = "6.1.0"
        shedlockVersion = "4.1.0"
    }

    repositories {
        mavenLocal()
        maven { url MAVEN_REPO_URL }
        maven { url "https://repo.spring.io/libs-milestone" }
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

allprojects {
    group "com.tencent.bkrepo"
    version "0.7.1"

    repositories {
        mavenLocal()
        maven { url MAVEN_REPO_URL }
        maven { url "https://repo.spring.io/libs-milestone" }
        mavenCentral()
        jcenter()
    }

}

subprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: 'org.springframework.boot'
    apply plugin: "io.spring.dependency-management"
    apply plugin: "maven-publish"


    def isBootProject = project.name.startsWith("boot-")

    jar.enabled = !isBootProject
    bootJar.enabled = isBootProject
    bootRun.enabled = isBootProject


    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    tasks.withType(JavaCompile).all {
        options.encoding = "UTF-8"
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(Test).all {
        useJUnitPlatform()
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint"

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"

        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude module: "junit"
        }
        testImplementation("org.junit.jupiter:junit-jupiter-api")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt", "!src/**/*Test.kt"
        // see https://github.com/pinterest/ktlint#usage for more
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt", "!src/**/*Test.kt"
    }


    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            }
            dependency "com.pinterest:ktlint:$ktlintVersion"
            dependency "io.swagger:swagger-annotations:$swaggerCoreVersion"
            dependency "io.swagger:swagger-models:$swaggerCoreVersion"
            dependency "io.springfox:springfox-swagger2:$swaggerFoxVersion"
            dependency "com.github.ulisesbocchio:jasypt-spring-boot-starter:$jasyptVersion"
            dependency "com.amazonaws:aws-java-sdk-s3:$awsS3Version"
            dependency "com.tencent:innercos-java-sdk:$innerCosVersion"
            dependency "com.google.guava:guava:$guavaVersion"
            dependency "commons-codec:commons-codec:$commonsCodecVersion"
            dependency "commons-io:commons-io:$commonsIoVersion"
            dependency "commons-fileupload:commons-fileupload:$commonsUploadVersion"
            dependency "org.apache.skywalking:apm-toolkit-logback-1.x:$skywalkingVersion"
            dependency "org.apache.skywalking:apm-toolkit-trace:$skywalkingVersion"
            dependency "net.javacrumbs.shedlock:shedlock-spring:$shedlockVersion"
            dependency "net.javacrumbs.shedlock:shedlock-provider-mongo:$shedlockVersion"
        }

    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url = MAVEN_REPO_SNAPSHOT_DEPLOY_URL
                    credentials {
                        username MAVEN_REPO_SNAPSHOT_USERNAME
                        password MAVEN_REPO_SNAPSHOT_PASSWORD
                    }
                } else {
                    url = MAVEN_REPO_DEPLOY_URL
                    credentials {
                        username MAVEN_REPO_USERNAME
                        password MAVEN_REPO_PASSWORD
                    }

                }
            }
        }
    }
}

task publishApiJar(group: "publishing") {
    def projectList = [
        ":common:common-api",
        ":common:common-artifact:artifact-api",
        ":common:common-query:query-api",
        ":common:common-storage:storage-api",
        ":generic:api-generic",
        ":repository:api-repository"
    ]
    projectList.each { dependsOn project(it).tasks.publish }
}
